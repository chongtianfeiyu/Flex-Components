<?xml version="1.0" encoding="utf-8"?>
<s:Button xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark" click="openList()" creationComplete="init(event)"
		  skinClass="common.widgets.buttonbox.skins.ButtonBoxSkin">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Metadata>
			[Event(name="opened", type="common.widgets.buttonbox.events.ButtonBoxEvent")]
			[Event(name="closed", type="common.widgets.buttonbox.events.ButtonBoxEvent")]
			[Event(name="selected", type="common.widgets.buttonbox.events.ButtonBoxEvent")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import common.widgets.buttonbox.events.ButtonBoxEvent;
			
			import mx.collections.ArrayCollection;
			import mx.collections.IList;
			import mx.events.FlexEvent;
			import mx.events.ItemClickEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.Label;
			import spark.components.List;
			import spark.components.SkinnablePopUpContainer;
			import spark.components.View;
			import spark.events.IndexChangeEvent;
			
			private var list:ButtonBoxImpl;
		
			private var _prompt:String = "点击选择";
			private var _dp:IList;
			private var _selected:Object;
			private var _selectedIndex:int;
 
			private var _lblField:String;
			
			//Skin部分需要声明的对象，required = ture表示Skin必须声明该对象，我们使用这个对象来显示选中的值
			[SkinPart(required="true")]
			public var promptDisplay:Label;
			
			[Bindable]
			public function get dataProvider():IList
			{
				return _dp;
			}
			public function set dataProvider(value:IList):void
			{
				_dp = value;
			}
			
			[Bindable]
			public function get labelField():String
			{
				return _lblField;
			}
			public function set labelField(value:String):void
			{
				_lblField = value;
			}
			
			[Bindable]
			public function get selectedItem():Object
			{
				return _selected;
			}
			public function set selectedItem(value:Object):void
			{
				_selected = value;
			}
			
			[Bindable]
			public function get selectedIndex():int
			{
				return _selectedIndex;
			}
			public function set selectedIndex(value:int):void
			{
				_selectedIndex = value;
			}
			
			[Bindable]
			public function get prompt():String
			{
				return _prompt;
			}
			public function set prompt(value:String):void
			{
				_prompt = value;	
			}		
			
			//CreationComplete时设置属性和控件布局,默认prompt是Touch to select
			protected function init(event:FlexEvent):void
			{			
				label = _prompt;
				invalidateProperties();
				invalidateDisplayList();
			}
		
			//下拉菜单显示默认值
			override protected function commitProperties():void
			{
				super.commitProperties();
				promptDisplay.text = label;
			}

			//点击下拉菜单时弹出List。
			protected function openList():void
			{
				var listData:ArrayCollection;
				var listArr:Array=new Array;
				var temp:Object;
				for(var a:int=0;a<dataProvider.length;a++){
					temp=dataProvider[a];
					listArr.push(temp[labelField]);
				}
				listData=new ArrayCollection(listArr);
				list = new ButtonBoxImpl();
				list.open(this,true);
				list.x = (screen.width - list.width)/2;
				list.y = (screen.height - list.height)/2;
				list._title.text = _prompt;
				list.dataProvider = listData;
				list.addEventListener(IndexChangeEvent.CHANGE,selectionHandler,true,1,true);
				dispatchEvent(new ButtonBoxEvent(ButtonBoxEvent.OPENED,true,true));
			}
			
			//选中item后响应，显示选中的值，发出Selected事件
			protected function selectionHandler(event:IndexChangeEvent):void
			{
				_selected = list.selectedItem;
				_selectedIndex = list.selectedIndex;
				label = String(list._optionsList.selectedItem);
				invalidateProperties();
				dispatchEvent(new ButtonBoxEvent(ButtonBoxEvent.SELECTED,true,true));
				list.close();
				list = null;
			}
			
		]]>
		</fx:Script>
</s:Button>
